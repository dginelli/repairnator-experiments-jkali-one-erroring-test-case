PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

select
    (?fr AS ?personId)
    (?first AS ?personFirstName)
    (?last AS ?personLastName)
    (?post AS ?commentOrPostId)
    (?content AS ?commentOrPostContent)
    (?date AS ?commentOrPostCreationDate)
where {
  {
    select distinct ?fr
    where {
      { sn:pers$Person snvoc:knows ?fr . }
      union
      {
        sn:pers$Person snvoc:knows ?fr2 .
        ?fr2 snvoc:knows ?fr .
        filter (?fr != sn:pers$Person)
      }
    }
  }
  ?fr snvoc:firstName ?first .
  ?fr snvoc:lastName ?last .
  ?post snvoc:hasCreator ?fr .
  ?post snvoc:creationDate ?date .
  filter (?date < $Date0) .
  {{?post snvoc:content ?content} union {?post snvoc:imageFile ?content}} .
}
order by desc(?creationDate) ?post
limit 20

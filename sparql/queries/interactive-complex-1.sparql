PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

select
    (?fr AS ?friendId)
    (?last AS ?friendLastName)
    (min(?dist) AS ?distanceFromPerson)
    (?bday AS ?friendBirthday)
    (?since AS ?friendCreationDate)
    (?gen AS ?friendGender)
    (?browser AS ?friendBrowserUsed)
    (?locationIP AS ?friendLocationIp)
    (((select group_concat (?email, ", ")
      where {
            ?frr snvoc:email ?email .
            filter (?frr = ?fr) .
      }
      group by ?frr)) AS ?friendEmails)
    (((select group_concat (?lng, ", ")
      where {
            ?frr snvoc:speaks ?lng .
            filter (?frr = ?fr) .
      }
      group by ?frr)) AS ?friendLanguages)
    (?based AS ?friendCityName)
    (
      ((select group_concat ( bif:concat (?o_name, " ", ?year, " ", ?o_country), ", ")
      where {
            ?frr snvoc:studyAt ?w .
            ?w snvoc:classYear ?year .
            ?w snvoc:hasOrganisation ?org .
            ?org snvoc:isLocatedIn ?o_countryURI .
            ?o_countryURI foaf:name ?o_country .
            ?org foaf:name ?o_name .
            filter (?frr = ?fr) .
      }
      group by ?frr)) AS ?friendUniversities)
    (
      ((select group_concat ( bif:concat (?o_name, " ", ?year, " ", ?o_country), ", ")
      where {
            ?frr snvoc:workAt ?w .
            ?w snvoc:workFrom ?year .
            ?w snvoc:hasOrganisation ?org .
            ?org snvoc:isLocatedIn ?o_countryURI .
            ?o_countryURI foaf:name ?o_country .
            ?org foaf:name ?o_name .
            filter (?frr = ?fr) .
      }
      group by ?frr)) AS ?friendCompanies)
{
    ?fr a snvoc:Person . ?fr snvoc:firstName $Name . ?fr snvoc:lastName ?last .
    ?fr snvoc:birthday ?bday . ?fr snvoc:isLocatedIn ?basedURI . ?basedURI foaf:name ?based .
    ?fr snvoc:creationDate ?since . ?fr snvoc:gender ?gen . ?fr snvoc:locationIP ?locationIP .
    ?fr snvoc:browserUsed ?browser .
    {
      { select distinct ?fr (1 as ?dist)
        where {
          sn:pers$Person snvoc:knows ?fr.
        }
      }
      union
      { select distinct ?fr (2 as ?dist)
        where {
          sn:pers$Person snvoc:knows ?fr2. ?fr2 snvoc:knows ?fr. filter (?fr != sn:pers$Person).
        }
      }
      union
      { select distinct ?fr (3 as ?dist)
        where {
          sn:pers$Person snvoc:knows ?fr2. ?fr2 snvoc:knows ?fr3. ?fr3 snvoc:knows ?fr. filter (?fr != sn:pers$Person).
        }
      } .
    }
}
group by ?fr ?last ?bday ?since ?gen ?browser ?locationIP ?based
order by ?mindist ?last ?fr
limit 20

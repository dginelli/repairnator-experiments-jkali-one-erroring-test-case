buildscript {
  ext {
    javaVersion = "1.5"
    vavrVersion = "0.9.2"
    reflectionsVersion = "0.9.11"
    lombokVersion = "1.16.20"
    logbackVersion = "1.2.3"
    slf4jVersion = "1.7.25"
    junitVersion = "4.12"
    injectVersion = "1"

    // publication
    vcsUrl = "https://github.com/daggerok/daggerok-context.git"
  }
}

plugins {
  id "idea"
  id "java"
  id "maven"
  id "eclipse"
  id "com.jfrog.bintray" version "1.8.0"
}

description = "Daggerok Context - Java dependency injection library."
group = "com.github.daggerok"
//group = "daggerok"
version = "0.0.1"

defaultTasks "clean", "build", "install", "publishToMavenLocal", "publish"

sourceCompatibility = targetCompatibility = "$javaVersion"

allprojects {
  repositories {
    mavenLocal()
    mavenCentral()
  }
}

dependencies {

  // Reflections
  compile("org.reflections:reflections:$reflectionsVersion")

  // JSR-330
  compile("javax.inject:javax.inject:$injectVersion")

  // In java we trust...
  compileOnly("org.projectlombok:lombok:$lombokVersion")
  testCompileOnly("org.projectlombok:lombok:$lombokVersion")

  // Logging
  compile("org.slf4j:slf4j-api:$slf4jVersion")
  compile("org.slf4j:jul-to-slf4j:$slf4jVersion")
  compile("org.slf4j:log4j-over-slf4j:$slf4jVersion")
  compile("ch.qos.logback:logback-classic:$logbackVersion")
  //compile("org.slf4j:slf4j-jdk14:$slf4jVersion")

  // Testing
  testCompile("junit:junit:$junitVersion")
}

apply plugin: "maven-publish"

task classifierJar(type: Jar) {
  from sourceSets.main.output
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

task classifierSourceJar(type: Jar) {
  from sourceSets.main.allJava
}

def pomConfig = {
  scm {
    url "$project.vcsUrl"
  }
  developers {
    developer {
      id "daggerok"
      name "Maksim Kostromin"
      email "daggerok@gmail.com"
    }
  }
  licenses {
    license {
      name "MIT License"
      url "https://raw.githubusercontent.com/daggerok/daggerok-context/master/LICENSE"
      distribution "repo"
    }
  }
}

publishing {
  publications {
    publication(MavenPublication) {
      from components.java
      artifact classifierJar { classifier = "1.5" }
      artifact sourceJar { classifier = "sources" }
      artifact classifierSourceJar { classifier = "1.5-sources" }
      pom.withXml {
        def root = asNode()
        root.appendNode("description", project.description)
        root.appendNode("name", project.name)
        root.appendNode("url", project.vcsUrl)
        root.children().last() + pomConfig
      }
    }
  }
  repositories {
    maven {
      url "$buildDir/maven-publish"
    }
  }
}

bintray {
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
  publications = ["publication"]
  pkg {
    userOrg = user
    repo = "daggerok"
    name = "$project.name"
    licenses = ["MIT"]
    vcsUrl = project.vcsUrl
    labels = [
        "Dependency Injection",
        "Inversion of Control",
        "Context Dependency Injection",
        "dependency-injection",
        "inversion-of-control",
        "context-dependency-injection",
        "daggerok-context",
        "context",
        "CDI",
        "cdi",
        "IoC",
        "ioc",
    ]
    publicDownloadNumbers = true
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = "4.6"
  distributionType = "ALL"
}

clean.doFirst {
  ["out"].each {
    delete "$projectDir/$it"
  }
}
